;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;Copyright (c) 2013 Manolis Agkopian		    ;
;See the file LICENCE for copying permission.	    ;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	PROCESSOR 16F876A
	INCLUDE <P16F876A.INC>

	__config _XT_OSC & _WDT_OFF & _PWRTE_OFF & _CP_OFF & _LVP_OFF & _BODEN_OFF

TXREG_T EQU 0x20
CNT0 EQU 0x21
CNT1 EQU 0x22

	ORG 0x00
INIT:
	;---INIT PORTS
	;CLEAR THE PORTS
	CLRF PORTB
	CLRF PORTC
	
	BSF STATUS, RP0 ;SWITCH TO BANK 01
	
	CLRF TRISC ;SET ALL PINS OF PORTC AS OUTPUT
	MOVLW B'00010000' ;SET PB4 AS INPUT AND THE REST PORTB AS OUTPUT
	MOVWF TRISB
	
	MOVLW B'00000111' ;DISABLE COMPARATORS MODULES
	MOVWF CMCON

	BCF OPTION_REG, 7 ;ENABLE PULL-UP RESISTORS OF PORTB

	
	;---CONFIGURE SPBRG FOR DESIRED BAUD RATE
	MOVLW D'51' ;WE WILL USE 4800bps 
	MOVWF SPBRG ;BAUD AT 4MHZ

	
	;---CONFIGURE TXSTA
	MOVLW B'00100100' ;CONFIGURE TXSTA AS :
	MOVWF TXSTA ;
	;8 BIT TRANSMISSION - 6.BIT
	;TRANSMIT ENABLED - 5.BIT
	;ASYNCHRONOUS MODE - 4.BIT
	;ENABLE HIGH SPEED BAUD RATE - 2.BIT

	BCF STATUS, RP0 ;SWITCH TO BANK 00
	
	MOVLW B'10000000' ;ENABLE SERIAL PORT
	MOVWF RCSTA ;RECEIVE STATUS REG
	
	MOVLW 0x20
	MOVWF TXREG_T ;TO TXREG

MAIN:
	BTFSC PORTB, 4 ;CHECK IF THE BUTTON IS PRESSED (BECOMES LOW FROM HIGH, WE USE PULL-UP RESITORS NOT PULL-DOWN)
	GOTO MAIN ;IF NOT CONTINUE TO LOOP
	
	;ELSE TRANSMIT A BYTE
	INCF TXREG_T
	MOVF TXREG_T, W
	XORLW 0x7F
	BTFSS STATUS, Z
	GOTO NEXT_0
	MOVLW 0x21 ;'!'
	MOVWF TXREG_T ;TO TXREG
NEXT_0:	
	MOVF TXREG_T, W
	MOVWF TXREG ;TO TXREG
	
TRANSWT:
	BSF STATUS, RP0
WTHERE:
	BTFSS TXSTA, TRMT
    GOTO WTHERE

    BCF STATUS, RP0
	
	CALL DELAY
			
	GOTO MAIN

DELAY:
	MOVLW 0xF0
	MOVWF CNT1
D1:
	MOVLW 0xFA
	MOVWF CNT0
D0:
	DECFSZ CNT0
	GOTO D0
	
	DECFSZ CNT1
	GOTO D1

	RETURN

	END

